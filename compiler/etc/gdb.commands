set $forcecmd = 0
define hook-run
  if ($forcecmd != 1)
    if (tmpdirname != 0)
      call cleanup_for_exit()
    end
  end
  set $forcecmd = 0
end
define hook-quit
  if ($forcecmd != 1)
    if (tmpdirname != 0)
      call cleanup_for_exit()
    end
  end
  set $forcecmd = 0
end
define halt
  set $forcecmd = 1
  quit
end
define rerun
  set $forcecmd = 1
  run
end
define aid
 p aid($arg0)
end
define view
  call print_view($arg0)
end
define iview
  call iprint_view($arg0)
end
define nview
  call nprint_view($arg0)
end
define lview
  call list_view($arg0)
end
define flags
  call viewFlags($arg0)
end
define loc
 p ::stringLoc($arg0)
end
define locid
  # evaluate $arg0 only once
  set $eval_temp = ($arg0)
  printf "%d  %s\n", debugID($eval_temp), debugLoc($eval_temp)
end
break gdbShouldBreakHere

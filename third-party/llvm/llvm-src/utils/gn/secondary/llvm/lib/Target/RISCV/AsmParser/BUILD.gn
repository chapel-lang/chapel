import("//llvm/utils/TableGen/tablegen.gni")

tablegen("RISCVGenAsmMatcher") {
  visibility = [
    ":AsmParser",
    "//llvm/lib/Target/RISCV:LLVMRISCVCodeGen",
  ]
  args = [ "-gen-asm-matcher" ]
  td_file = "../RISCV.td"
}

static_library("AsmParser") {
  output_name = "LLVMRISCVAsmParser"
  deps = [
    ":RISCVGenAsmMatcher",
    "//llvm/lib/MC",
    "//llvm/lib/MC/MCParser",
    "//llvm/lib/Support",
    "//llvm/lib/Target/RISCV:RISCVGenCompressInstEmitter",
    "//llvm/lib/Target/RISCV/MCTargetDesc",
    "//llvm/lib/Target/RISCV/Utils",
  ]
  include_dirs = [ ".." ]
  sources = [
    # Make `gn format` not collapse this, for sync_source_lists_from_cmake.py.
    "RISCVAsmParser.cpp",
  ]
}
